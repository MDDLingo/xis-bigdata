-- @atlcompiler atl2006
-- @nsURI Ecore=http://www.eclipse.org/emf/2002/Ecore
-- @nsURI UML=http://www.eclipse.org/uml2/4.0.0/UML

module Ecore2UML;
create OUT: UML from IN: Ecore, IN_Library: UML, IN_XISMobile: UML;

helper def : parentModel : UML!Model = OclUndefined;

helper def: getStereotype(name: String): UML!Stereotype =
	UML!Stereotype.allInstancesFrom('IN_XISMobile') -> select(p | p.name = name) -> first();

rule EPackage2Package {
	from e: Ecore!EPackage
	to
		u: UML!Package (
			name <- e.name,
			packagedElement <- e.eClassifiers
		)
	do {
		thisModule.Model(u);
	}
}

unique lazy rule Model {
	from e: UML!Package
	to
		u: 	UML!Model (
			name <- e.name + 'Model',
			packagedElement <- e
		)
	do {
		u.applyProfile(UML!Profile.allInstancesFrom('IN_XISMobile').first());
		UML!Profile.allInstancesFrom('IN_XISMobile').debug();
		thisModule.parentModel <- u;
	}
}

rule EClass2Class {
	from e: Ecore!EClass
	to
		u: UML!Class (
			name <- e.name,
			ownedAttribute <- Sequence {
				e.eAttributes,
				e.eReferences->select(e | e.eOpposite.oclIsUndefined())
			}
		)
	do {
		u.applyStereotype(thisModule.getStereotype('XisEntity'));
		u.setValue(thisModule.getStereotype('XisEntity'), 'name', e.name);
	}
}

helper def : getMappingType(input : Ecore!ETypedElement) : UML!Type =
if(input.name = 'EString') then UML!PrimitiveType.allInstancesFrom('IN_Library')->any(e|e.name='String')
else if(input.name = 'EBoolean') then UML!PrimitiveType.allInstancesFrom('IN_Library')->any(e|e.name='Boolean')
else if(input.name = 'EInt') then UML!PrimitiveType.allInstancesFrom('IN_Library')->any(e|e.name='Integer')
else if(input.name = 'EDouble') then UML!PrimitiveType.allInstancesFrom('IN_Library')->any(e|e.name='Real')
else OclUndefined
endif endif endif endif;

rule EAttribute2Property {
	from e: Ecore!EAttribute
	to
		u: UML!Property (
			name <- e.name,
			type <- thisModule.getMappingType(e.eType),
			upper <- e.upperBound,
			lower <- e.lowerBound
		)
	do {
		u.applyStereotype(thisModule.getStereotype('XisEntityAttribute'));
	}
}

rule EReference2Property {
	from e: Ecore!EReference (e.eOpposite.oclIsUndefined())
	to
		u: UML!Property (
			name <- e.name,
			type <- e.eType,
			upper <- e.upperBound,
			lower <- e.lowerBound
		)
}

rule EReferenceEOpposite2Property {
	from e: Ecore!EReference (not e.eOpposite.oclIsUndefined())
	to
		u: UML!Property (
			name <- e.name,
			upper <- e.upperBound,
			lower <- e.lowerBound,
			association <- thisModule.Association(e, e.eOpposite),
			type <- e.eType
		)
	do {
		if (e.containment and u.association.ownedEnd->size() > 0) {
			u.association.ownedEnd.last().aggregation <- #composite;	
		}
--		e.debug();
--		e.eOpposite.debug('Opposite');
--		e.eType.debug();
--		'--'.debug();
	}
}

unique lazy rule Association {
	from e1: Ecore!EReference, e2: Ecore!EReference
	to
		u: 	UML!Association (
			name <- e1.name + '_' + e2.name,
			memberEnd <- Sequence {e1, e2},
			ownedEnd <- Sequence {e1, e2}
		)
	do {
		thisModule.parentModel.packagedElement <- thisModule.parentModel.packagedElement.append(u);
		u.applyStereotype(thisModule.getStereotype('XisEntityAssociation'));
		u.setValue(thisModule.getStereotype('XisEntityAssociation'), 'roleName', e1.name + '_' + e2.name);
	}
}

rule EDataType2DataType {
	from e: Ecore!EDataType
	to
		u: UML!DataType (
			name <- e.name
		)
}

--endpoint rule ApplyAllStereotypes() {
--	do {
--		for (element in thisModule.parentModel.allOwnedElements()
--				->select(o|o.oclIsKindOf(UML!"uml::Class"))) {
--			element.applyStereotype(thisModule.getStereotype('XisEntity'));
--			element.getApplicableStereotypes().debug();
--		}
--	}
--}
